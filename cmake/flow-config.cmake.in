# -------------------------------------------------------------------------------------------------- #
# --------------------------------- Check that called with component ------------------------------- #
# -------------------------------------------------------------------------------------------------- #
if (NOT flow_FIND_COMPONENTS)
    message(FATAL_ERROR "find_package(flow) called with no component")
endif()
# -------------------------------------------------------------------------------------------------- #
# ---------------------------- Set config_name dependent on libraries type ------------------------- #
# -------------------------------------------------------------------------------------------------- #
if(FLOW_STATIC_LIBRARIES)
    set(config_name "static")
else()
    set(config_name "shared")
endif()
# -------------------------------------------------------------------------------------------------- #
# ------------------------------------ Update requested components --------------------------------- #
# -------------------------------------------------------------------------------------------------- #
set(FIND_flow_ADDITIONAL_COMPONENTS "")

foreach(component ${flow_FIND_COMPONENTS})
    list(APPEND FIND_flow_ADDITIONAL_COMPONENTS ${FIND_flow_${component}_DEPENDENCIES})
endforeach()

list(APPEND flow_FIND_COMPONENTS ${FIND_flow_ADDITIONAL_COMPONENTS})
list(REMOVE_DUPLICATES flow_FIND_COMPONENTS)
# -------------------------------------------------------------------------------------------------- #
# ----------------------------------- Load targets and dependencies -------------------------------- #
# -------------------------------------------------------------------------------------------------- #
set(targets_config_file ${CMAKE_CURRENT_LIST_DIR}/flow-${config_name}-targets.cmake)
set(flow_FOUND TRUE)

if(EXISTS ${targets_config_file})
    include(${targets_config_file})
    include("${CMAKE_CURRENT_LIST_DIR}/flow-config-dependencies.cmake")

    if (flow_FOUND)
        foreach (component ${flow_FIND_COMPONENTS})
            if (TARGET flow-${component})
                set(flow_${component}_FOUND TRUE)
            else()
                set(FIND_flow_ERROR "Found flow but requested component '${component}' is missing in the config defined in ${FLOW_DIR}.")
                set(flow_${component}_FOUND FALSE)
                set(flow_FOUND FALSE)
            endif()
        endforeach()
    endif()
else()
    set(FIND_flow_ERROR "Requested flow configuration (${config_name}) was not found")
    set(flow_FOUND FALSE)
endif()
# -------------------------------------------------------------------------------------------------- #
# --------------------------------------- Display all errors --------------------------------------- #
# -------------------------------------------------------------------------------------------------- #
if (NOT flow_FOUND)
    if(flow_FIND_REQUIRED)
        message(FATAL_ERROR "${FIND_flow_ERROR}")
    elseif(NOT flow_FIND_QUIETLY)
        message(STATUS "${FIND_flow_ERROR}")
    endif()
endif()
# -------------------------------------------------------------------------------------------------- #
